plugins {
	id 'java-library'
	id 'maven-publish'
	id 'me.champeau.jmh' version '0.6.5'
}

group = 'brownshome'
version = '3.5.0-vector'

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.encoding = 'utf-8'
	options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

javadoc {
	options.encoding = 'utf-8'
}

sourceSets {
	main.java.srcDirs = ['src/main']
	test.java.srcDirs = ['src/test']
	jmh.java.srcDirs = ['src/jmh']
}

jmh {
	includes = ['\\.(factorise|solve|divide)']

	benchmarkMode = ['AverageTime']
	timeUnit = 'us'
	fork = 1

	jvmArgsPrepend = ['--add-modules jdk.incubator.vector']
}

jmhRunBytecodeGenerator {
	jvmArgs = ['--add-modules', 'jdk.incubator.vector']
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/Vecmath')
			credentials {
				println project.getProperties()
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'vecmath'
			from components.java

			pom {
				name = 'Vecmath'
				description = 'A mutable vector mathematics library'
				url = 'https://github.com/Brownshome/Vecmath'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/Vecmath.git'
					url = 'https://github.com/Brownshome/Vecmath'
				}
			}
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

test {
	useJUnitPlatform()
	enableAssertions = true
	jvmArgs += [
			'--add-modules',
			'jdk.incubator.vector'
	]
}