plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'brownshome'
version = '2.1.0'

repositories {
	mavenCentral()
}

java {
	modularity.inferModulePath = true

	withJavadocJar()
	withSourcesJar()
}

sourceSets {
	main.java.srcDirs = ['src/main']
	test.java.srcDirs = ['src/test']
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/Vecmath')
			credentials {
				println project.getProperties()
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'vecmath'
			from components.java

			pom {
				name = 'Vecmath'
				description = 'A mutable vector mathematics library'
				url = 'https://github.com/Brownshome/Vecmath'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/Vecmath.git'
					url = 'https://github.com/Brownshome/Vecmath'
				}
			}
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

test {
	useJUnitPlatform()
}